
// Launch from the current folder
// ../../../framework/build_oracle/oracletool AA1 ./ -flag_circuit 1 -flag_tex 1 -flag_compute_output 1 -flag_print_output  0  -tex_CL  10  

// estimation of the eigenphase of the single-qubit operator Ry;

CONSTANTS
na 1   // A is a single-qubit operator;
ny 8
th 4.2
hpi  1.5707963267948966
END_CONSTANTS



CIRCUITS_DECLARATION
A    1           a <na> 0  // operator whose eigenphase to find;
INIT 1           a <na> 0  // operator to create the eigenstate of the operator "A";
U    3  c 2 1 y <ny> 0 a <na> 0   // an envelop circuit to create the phase estimation operator;
END_CIRCUITS_DECLARATION

INPUT_STATES   U        
STATE     3   c 1 y 0 a 0     end_register  
END_INPUT_STATES


// A = Ry(th), eigenvalues are exp(+- i * th/2) -> eigenphases = +- th/2;
// we choose the eigenphase = th/2 by constructing the appropriate eigenvector 
//   0.707 * [i, 1]
// by using the operator INIT.

CIRCUIT_STRUCTURE A 
gate Ry a 1  <th> end_gate
END_CIRCUIT_STRUCTURE

CIRCUIT_STRUCTURE INIT
gate H     a 1       end_gate
gate X     a 1       end_gate
gate Phase a 1 <hpi> end_gate
gate X     a 1       end_gate
END_CIRCUIT_STRUCTURE


CIRCUIT_STRUCTURE U
gate  PE  a -1  A  INIT  y -1 control c 1 ocontrol c 2 end_gate
END_CIRCUIT_STRUCTURE



























 











