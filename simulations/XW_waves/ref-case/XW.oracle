//-> Reference case with Nx = 1024
CONSTANTS  
nj  10    //-> size of a space grid
jq1 460  jq1x -461  //-> <jq1x> = -<jq1> - 1: source position point 1 
jq2 461  jq2x -462  //-> <jq2x> = -<jq2> - 1: source position point 2
angle_sqrt_beta  2.855   //-> <angle_sqrt_beta> = 2*arccos(sqrt(bAP)), where 
			 	//-> bAP - the antenna-plasma coupling coefficient 
angle_wa 2.575 //-> <angle_wa> = 2*arccos(sqrt(wa)), where wa - antenna frequency
END_CONSTANTS


CIRCUITS_DECLARATION
VXY 4        aj 2 1 ad 3 1 d 3 0 j <nj> 0
EY  4        aj 2 1 ad 3 1 d 3 0 j <nj> 0
Bz  4        aj 2 1 ad 3 1 d 3 0 j <nj> 0
Q   4        aj 2 1 ad 3 1 d 3 0 j <nj> 0
OF  4        aj 2 1 ad 3 1 d 3 0 j <nj> 0
OM  4        aj 2 1 ad 3 1 d 3 0 j <nj> 0
OS  5  a 1 1 aj 2 1 ad 3 1 d 3 0 j <nj> 0
OH  5  a 1 1 aj 2 1 ad 3 1 d 3 0 j <nj> 0
U   5  a 2 1 aj 2 1 ad 3 1 d 3 0 j <nj> 0
END_CIRCUITS_DECLARATION

LAUNCH_SECTION
sel_launch  full
//-> flag_draw   U   	
//-> flag_redraw U  
END_LAUNCH_SECTION

//-> INPUT_STATES  U    
STATE 1     j  <jq1> end_register //-> 0: vx: bulk
STATE 0              end_register //-> 1: vx: boundary 0 
STATE 1     j -1     end_register //-> 2: vx: boundary Nx-1
STATE 2 d 1 j  <jq2> end_register //-> 3: vy: bulk
STATE 1 d 1          end_register //-> 4: vy: boundary 0
STATE 2 d 1 j -1     end_register //-> 5: vy: boundary (Nx-1)
END_INPUT_STATES

//-> INPUT_STATES   U         
STATE 2 d 2 j  0     end_register //-> 0: Ex 
STATE 2 d 2 j  5     end_register //-> 1: Ex      
STATE 2 d 2 j  <jq1> end_register //-> 2: Ex
STATE 2 d 2 j -1     end_register //-> 3: Ex
END_INPUT_STATES

//-> INPUT_STATES   U             
STATE 2 d 3 j  <jq1> end_register //-> 0: Ey: bulk
STATE 2 d 3 j  8     end_register //-> 1: Ey: bulk
STATE 1 d 3          end_register //-> 2: Ey: boundary 0
STATE 2 d 3 j -1     end_register //-> 3: Ey: boundary Nx-1
STATE 2 d 3 j  1     end_register //-> 4: Ey: boundary 1
STATE 2 d 3 j -2     end_register //-> 5: Ey: boundary Nx-2
END_INPUT_STATES

INPUT_STATES   U             
STATE 2 d 4 j 3      end_register  //-> 0: Bz: bulk
STATE 2 d 4 j <jq1>  end_register  //-> 1: Bz: source 
STATE 2 d 4 j <jq2>  end_register  //-> 2: Bz: source 
STATE 1 d 4          end_register  //-> 3: Bz: boundary 0
STATE 2 d 4 j -1     end_register  //-> 4: Bz: boundary Nx-1
STATE 2 d 4 j  1     end_register  //-> 5: Bz: boundary 1
STATE 2 d 4 j -2     end_register  //-> 6: Bz: boundary Nx - 2
END_INPUT_STATES

//-> INPUT_STATES   U             
STATE 2 d 5 j  2      end_register  //-> 0: Q: bulk
STATE 2 d 5 j  <jq1>  end_register  //-> 1: Q: source 
STATE 2 d 5 j  <jq2>  end_register  //-> 2: Q: source
STATE 1 d 5           end_register  //-> 3: Q: boundary 0
STATE 2 d 5 j -1      end_register  //-> 4: Q: boundary Nx - 1
END_INPUT_STATES

CIRCUIT_STRUCTURE VXY 
//-> for zero-control on d12
gate X d 6 end_gate
//-> bulk Hadamard
gate H ad 1 control d 6 end_gate
//-> bulk CNOT for vx
gate X ad 2 control d 6 ocontrol 2 ad 1 d 1 end_gate
//-> boundary 
gate H ad 1 control 2 d -1 j -1 end_gate
gate X ad 2 control 3 ad 1 d -1 j -1 end_gate
gate X j -1 end_gate
gate H ad 1 control 2 d -1 j -1 end_gate
gate X ad 2 control 3 ad 1 d -1 j -1 end_gate
gate X j -1 end_gate
//-> bulk CNOT for vy
gate X ad 2 control 2 d -1 ad 1 end_gate
//-> end zero-control on d12
gate X d 6 end_gate
END_CIRCUIT_STRUCTURE


CIRCUIT_STRUCTURE EY
//-> for zero-control on d2
gate X d 4 end_gate
//-> bulk H
gate H ad 1 control d -1 end_gate
//-> bulk X on ad2
gate X ad 1 end_gate
gate X ad 4 control 2 d -1 ad 1 end_gate
//-> boundary X on ad2
gate X ad 4 control 3 d -1 j -1 ad 1                end_gate
gate X ad 4 control 2 d -1 ad 1       ocontrol j -1 end_gate
gate X ad 1 end_gate
//-> boundary H on ad0
gate H ad 1 control 2 d -1 j -1               end_gate
gate H ad 1 control   d -1      ocontrol j -1 end_gate
//-> boundary X on ad0 and ad1
gate X ad 3 control 2 d -1 j -1 end_gate
gate X j -1 end_gate
gate X ad 3 control 2 d -1 j -1 end_gate
gate X 2 ad 1 j 1 end_gate
//-> bulk H on aj0
gate H aj 1 control 2 ad 5 d -1  end_gate
//-> boundary H on aj0
gate H aj 1 control 3 ad 5 d -1 j -1 end_gate
gate X j -1 end_gate
gate H aj 1 control 3 ad 5 d -1 j -1 end_gate
//-> boundary X on aj0
gate X aj 1 control 3 ad 5 d -1 j -1 end_gate
//-> bulk X on aj1
gate X aj 2 control 2 ad 5 d -1 ocontrol aj 1 end_gate
//-> end zero-control on ad0, d2, j0
gate X 3 ad 1 d 4 j 1 end_gate
END_CIRCUIT_STRUCTURE


CIRCUIT_STRUCTURE BZ
//-> set zero-control on d01
gate X d 3 end_gate
//-> bulk CNOTs on ad01
gate X ad  3 control d -1 end_gate
//-> boundary CNOTs: Nx-1
gate X ad -1 control 2 d -1 j -1 end_gate
//-> boundary CNOTs: 0
gate X  j -1                     end_gate
gate X ad -1 control 2 d -1 j -1 end_gate
//-> B-SOURCE 1
gate X j <jq1> end_gate //-> here, we set <jq1> instead of <jq1x> because of the previous CNOTs
//-> X on ad1
gate X ad 2 control 2 d -1 j -1 end_gate
//-> H on ad1
gate H ad 2 control 2 d -1 j -1 end_gate
//-> X on ad2
gate X ad 4 control 2 d -1 j -1 ocontrol ad 2 end_gate
//-> E-SOURCE 1 
gate X j <jq1x> end_gate
//-> B-SOURCE 2
gate X j <jq2x> end_gate 
//-> X on ad1
gate X ad 2 control 2 d -1 j -1 end_gate
//-> H on ad1
gate H ad 2 control 2 d -1 j -1 end_gate
//-> X on ad2
gate X ad 4 control 2 d -1 j -1 ocontrol ad 2 end_gate
//-> E-SOURCE 2
gate X j <jq2x> end_gate
//-> bulk H on aj0:
gate H aj 1 control 2 ad 3 d -1 end_gate
//-> boundary H on aj0: 1
gate H aj 1 control 3 ad 3 d -1 j 1 ocontrol j -2 end_gate
//-> boundary H on aj0: -2
gate H aj 1 control 3 ad 3 d -1 j -2 ocontrol j 1 end_gate
//-> boundary X on aj0: -2
gate X aj 1 control 3 ad 3 d -1 j -2 ocontrol j 1 end_gate
//-> bulk X on aj1:
gate X aj 2 control 2 ad 3 d -1 ocontrol aj 1 end_gate
//-> end zero-control on d01
gate X d 3 end_gate
END_CIRCUIT_STRUCTURE


CIRCUIT_STRUCTURE Q
//-> set zero-control on d1
gate X d 2 end_gate
//-> bulk X on ad0 and ad2
gate X ad 5 control d -1 end_gate
//-> B-SOURCE 1
gate X j <jq1x> end_gate
//-> X on ad0
gate X ad 1 control 2 d -1 j -1 end_gate
//-> H on ad0
gate H ad 1 control 2 d -1 j -1 end_gate
//-> E-SOURCE 1
gate X j <jq1x> end_gate
//-> B-SOURCE 2
gate X j <jq2x> end_gate
//-> X on ad0
gate X ad 1 control 2 d -1 j -1 end_gate
//-> H on ad0
gate H ad 1 control 2 d -1 j -1 end_gate
//-> E-SOURCE 2
gate X j <jq2x> end_gate
//-> end zero-control on d1
gate X d 2 end_gate
END_CIRCUIT_STRUCTURE


CIRCUIT_STRUCTURE OF
circuit VXY -1 0 end_circuit
circuit EY  -1 0 end_circuit
circuit Bz  -1 0 end_circuit
circuit Q   -1 0 end_circuit
END_CIRCUIT_STRUCTURE


CIRCUIT_STRUCTURE OM
gate adder1      j -1 control aj 2 end_gate
gate subtractor1 j -1 control aj 1 end_gate
gate swap   d -1   ad -1  end_gate
END_CIRCUIT_STRUCTURE


CIRCUIT_STRUCTURE OS
//-> X on d2, ad12
gate X 2 d 4 ad 6 end_gate
//-> --- to get i ---
gate Rz a 1   9.4247779608 control 2 d  5 ad  6 ocontrol 2 d 2 ad 1 end_gate
gate Rz a 1   9.4247779608 control 2 d  6 ad  6 ocontrol 2 d 1 ad 1 end_gate
gate Rz a 1   9.4247779608 control 2 d -1 ad -1                     end_gate
gate Rz a 1   9.4247779608 control   aj 1                           end_gate
//-> ----------------
//-> remove X on d2, ad12
gate X 2 d 4 ad 6 end_gate
//-> X on d, ad2
gate X 2 d -1 ad 4 end_gate
//-> --- to get -i ---
gate Rz a 1   3.1415926536 control 2 d -1 ad 5 ocontrol ad 2 end_gate
gate Rz a 1   3.1415926536 control 2 d -1 ad 6 ocontrol ad 1 end_gate
gate X d 1 end_gate //-> remove X from d0
gate Rz a 1   3.1415926536 control 2 d -1 ad -1              end_gate 
gate Rz a 1   3.1415926536 control   aj 2                    end_gate
//-> -----------------
//-> remove X from d12, ad2
gate X 2 d 6 ad 4 end_gate
//-> X on d1, ad1
gate X 2 d 2 ad 2 end_gate
//-> --- to get -1 ---
gate Rz a 1   6.2831853072 control 2 d  6 ad -1 ocontrol  d 1 end_gate
gate Rz a 1   6.2831853072 control 2 d -1 ad  6 ocontrol ad 1 end_gate
//-> B-SOURCE 1
gate X j <jq1x> end_gate
gate Rz a 1   6.2831853072 control 3 d -1 ad -1 j -1          end_gate
//-> E-SOURCE 1
gate X j <jq1x> end_gate
//-> B-SOURCE 2
gate X j <jq2x> end_gate
gate Rz a 1   6.2831853072 control 3 d -1 ad -1 j -1          end_gate
//-> E-SOURCE 2
gate X j <jq2x> end_gate
//-> -----------------
//-> remove X from d1, ad1
gate X 2 d 2 ad 2 end_gate
END_CIRCUIT_STRUCTURE


CIRCUIT_STRUCTURE OH
//-> X on d, ad2
gate X 2 d -1 ad 4 end_gate
//-> B[0,1], n[0,2]
gate condR sqrt_Bvx  a 1 j -1 control 2 d -1 ad 5 ocontrol ad 2 end_gate
gate condR sqrt_nvxy a 1 j -1 control 2 d -1 ad 6 ocontrol ad 1 end_gate
//-> X on d0
gate X d 1 end_gate
//-> B[1,0], n[1,3]
gate condR sqrt_Bvy  a 1 j -1 control 2 d -1 ad  4 ocontrol ad 3 end_gate
gate condR sqrt_nvxy a 1 j -1 control 2 d -1 ad -1 end_gate
//-> X on d1
gate X d 2 end_gate
//-> n[2,0], n[3,0]
gate condR sqrt_nex a 1 j -1 control 2 d  6 ad 4 ocontrol 2 d 1 ad 3 end_gate
gate condR sqrt_ney a 1 j -1 control 2 d -1 ad 5 ocontrol 1     ad 2 end_gate
//-> X on d2, ad2
gate X 2 d 4 ad 4  end_gate
//-> 1/2h for Ey
gate condR sqrt_gey a 1 j -1 control 2 aj 1 d 3 end_gate
gate condR sqrt_gey a 1 j -1 control 2 aj 2 d 3 end_gate
//-> 1/2h for Bz
gate condR sqrt_gbz a 1 j -1 control 2 aj 1 d 4 end_gate
gate condR sqrt_gbz a 1 j -1 control 2 aj 2 d 4 end_gate
//-> X on d1, ad1
gate X 2 d 2 ad 2 end_gate
//-> B-SOURCE 1
gate X j <jq1x> end_gate
//-> coupling
gate Ry a 1   <angle_sqrt_beta>   control 3 d  6 ad -1 j -1 ocontrol  d 1 end_gate
gate Ry a 1   <angle_sqrt_beta>   control 3 d -1 ad  6 j -1 ocontrol ad 1 end_gate
//-> antenna frequency
gate Ry a 1   <angle_wa>   control 3 d -1 ad -1 j -1 end_gate
//-> E-SOURCE 1
gate X j <jq1x> end_gate
//-> B-SOURCE 2
gate X j <jq2x> end_gate
//-> coupling
gate Ry a 1   <angle_sqrt_beta>   control 3 d  6 ad -1 j -1 ocontrol  d 1 end_gate
gate Ry a 1   <angle_sqrt_beta>   control 3 d -1 ad  6 j -1 ocontrol ad 1 end_gate
//-> antenna frequency
gate Ry a 1  <angle_wa>   control 3 d -1 ad -1 j -1 end_gate
//-> E-SOURCE 2
gate X j <jq2x> end_gate
//-> X on d1, ad1
gate X 2 d 2 ad 2 end_gate
END_CIRCUIT_STRUCTURE





CIRCUIT_STRUCTURE U
circuit  OF 4 j -1 d -1 ad -1 aj -1      0 end_circuit
circuit  OH 5 j -1 d -1 ad -1 aj -1 a 1  0 end_circuit
circuit  OS 5 j -1 d -1 ad -1 aj -1 a 1  0 end_circuit
circuit  OM 4 j -1 d -1 ad -1 aj -1      0 end_circuit
icircuit OH 5 j -1 d -1 ad -1 aj -1 a 2  0 end_circuit
icircuit OF 4 j -1 d -1 ad -1 aj -1      0 end_circuit
END_CIRCUIT_STRUCTURE






















 











